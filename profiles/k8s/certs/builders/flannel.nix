{ pkgs
, lib
, config
, kubernetes-ca
}:

let
  reqCsr = builtins.toJSON {
    CN = "flannel-client";
    hosts = [ "flannel-client" ];
    inherit (config) key;
    names = [{}];
  };
in
pkgs.stdenvNoCC.mkDerivation {
  name = "certs-flannel";
  buildInputs = with pkgs; [
    cfssl
  ];

  caConfig = pkgs.writeText "caConfig.json" config.caConfigClient;
  inherit reqCsr;

  phases = [ "buildPhase" "installPhase" ];

  buildPhase = ''
    echo "$reqCsr" | \
    cfssl gencert \
      -ca ${kubernetes-ca}/kubernetes.pem \
      -ca-key ${kubernetes-ca}/kubernetes-key.pem \
      -config "$caConfig" \
      - | \
    cfssljson -bare flannel-apiserver-client
  '';

  installPhase = ''
    mkdir -p $out/flannel/
    cp *.pem $out/flannel/
  '';
}
